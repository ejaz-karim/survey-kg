PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX fx: <http://sparql.xyz/facade-x/ns/>
PREFIX pfn: <https://polifonia-project.eu/resource/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

CONSTRUCT {
    ?pilot_iri a pfn:Pilot ;
        rdfs:label ?pilot .
    ?pilot pfn:hasPart ?part_iri .
    ?part_iri rdfs:label ?part .
    ?part_iri pfn:hasQuestion ?question_iri .
    ?question_iri rdfs:label ?question .
    ?question_iri pfn:hasAnswer ?answer_iri .
    ?answer_iri rdfs:label ?answer .
}
WHERE {
    SERVICE <x-sparql-anything:file:///Users/amp/src/survey-kg/surveyExtraction.xlsx> {        
        ?r a fx:root ;
            ?p ?row .
        ?row rdf:_1 ?part ;
            rdf:_2 ?question ;
            rdf:_3 ?pilot ;
            rdf:_4 ?answer .
    }
    BIND(IRI(CONCAT("https://polifonia-project.eu/resource/pilot/", STR(ENCODE_FOR_URI(?pilot)))) as ?pilot_iri)
    BIND(IRI(CONCAT("https://polifonia-project.eu/resource/part/", STR(ENCODE_FOR_URI(?part)))) as ?part_iri)
    BIND(IRI(CONCAT("https://polifonia-project.eu/resource/question/", STR(ENCODE_FOR_URI(SHA1(?question))))) as ?question_iri)
    BIND(IRI(CONCAT("https://polifonia-project.eu/resource/answer/", STR(ENCODE_FOR_URI(SHA1(?answer))))) as ?answer_iri)
}
